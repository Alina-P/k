// This program tests multi-dimensional arrays.

function readArray(a : arrayOf arrayOf int) : void {
  var m:int = sizeOf(a) - 1, n:int = sizeOf(a[0]) - 1;
  for i = 0 to m do {
    print("Line ",i+1," (",n+1," elements): ");
    for j = 0 to n do
      a[i][j] = read();
  }
}

function printArray(a : arrayOf arrayOf int) : void {
  var m:int = sizeOf(a) - 1, n:int = sizeOf(a[0]) - 1;
  for i = 0 to m do {
    print("Line ",i+1," (",n+1," elements):");
    for j = 0 to n do
      print(" ",a[i,j]);
  print("\n");
  }
}

function transArray(a : arrayOf arrayOf int) : arrayOf arrayOf int {
  var  m:int = sizeOf(a) - 1, n:int = sizeOf(a[0]) - 1,
       b[n+1,m+1] : arrayOf arrayOf int;
  for i = 0 to m do
    for j = 0 to n do
      b[j,i] = a[i,j];
  return b;
}

function mulArray(a : arrayOf arrayOf int,
                  b : arrayOf arrayOf int) : arrayOf arrayOf int{
  var m:int = sizeOf(a) - 1, n:int = sizeOf(b) - 1, p:int = sizeOf(b[0]) - 1,
      c[m+1,p+1] : arrayOf arrayOf int;
  for i=0 to m do
    for j=0 to p do {
      c[i,j] = 0;
      for k=0 to n do
        c[i,j] = c[i,j] + a[i,k] * b[k,j];
    }
  return c;
}

function main() : void {
  print("Input the number of lines and columns (two natural numbers): ");
  var x:int = read(), y:int = read(), a[x,y] : arrayOf arrayOf int;
  readArray(a);
  print("Your matrix is:\n");
  printArray(a);
  print("The transpose of your matrix is:\n");
  printArray(transArray(a));
  print("You matrix multiplied with its transpose is:\n");
  printArray(mulArray(a,transArray(a)));
  print("The transpose of your matrix multiplied with your matrix is:\n");
  printArray(mulArray(transArray(a),a));
}

// Should output what it says.
